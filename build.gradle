plugins {
    id 'java'
    id 'application'
    id 'org.javamodularity.moduleplugin' version '1.8.12'
    id 'org.openjfx.javafxplugin' version '0.0.14'
    id 'org.beryx.jlink' version '2.25.0'
    id("com.github.johnrengelman.shadow") version "8.1.1"
}

group 'com.pranav'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

ext {
    junitVersion = '5.10.2'
}

java {
    sourceCompatibility = '21'
    targetCompatibility = '21'
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

application {
    mainModule = 'com.pranav.temple_software'
    mainClass = 'com.pranav.temple_software.Launcher'
}

javafx {
    version = '23.0.1'
    modules = ['javafx.controls', 'javafx.fxml', 'javafx.graphics']
}

dependencies {
    implementation 'com.h2database:h2:2.2.224'
    implementation 'org.apache.pdfbox:pdfbox:3.0.2'
    testImplementation("org.junit.jupiter:junit-jupiter-api:${junitVersion}")
    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:${junitVersion}")
    implementation 'org.xerial:sqlite-jdbc:3.45.3.0'
    implementation 'com.github.anastaciocintra:escpos-coffee:4.0.2'
}

test {
    useJUnitPlatform()
}

jlink {
    imageZip = project.file("${buildDir}/distributions/temple-software-${javafx.platform.classifier}.zip")

    options = [
            '--strip-debug',
            '--compress', '2',
            '--no-header-files',
            '--no-man-pages',
            '--bind-services',
            '--ignore-signing-information'
    ]

    // Force merge problematic dependencies
    forceMerge('h2')
    forceMerge('pdfbox')
    forceMerge('sqlite-jdbc')
    forceMerge('escpos-coffee')

    launcher {
        name = 'temple-software'
        jvmArgs = [
                '-Dfile.encoding=UTF-8',
                '--add-opens', 'java.base/java.lang=ALL-UNNAMED',
                '--add-opens', 'java.desktop/sun.awt=ALL-UNNAMED',
                '--add-opens', 'java.base/java.nio=ALL-UNNAMED',
                '--add-opens', 'java.base/sun.nio.ch=ALL-UNNAMED',
                '--add-opens', 'java.base/java.util=ALL-UNNAMED'
        ]
    }

    jpackage {
        imageName = 'Temple Software'
        installerName = 'temple-software-installer'

        if (org.gradle.internal.os.OperatingSystem.current().windows) {
            installerType = 'msi'
            installerOptions = [
                    '--win-dir-chooser',
                    '--win-shortcut',
                    '--win-menu',
                    '--win-per-user-install'
            ]
        }
        else if (org.gradle.internal.os.OperatingSystem.current().linux) {
            installerType = 'deb'
            installerOptions = [
                    '--linux-shortcut'
            ]
        }
        else if (org.gradle.internal.os.OperatingSystem.current().macOsX) {
            installerType = 'dmg'
        }

        appVersion = '1.0.0'
        vendor = 'Temple Management'
        description = 'Temple Management Software for Sri Shastara Subramanyeshwar Devasthana'
    }
}

jlinkZip {
    group = 'distribution'
}

// Alternative Shadow JAR task for fallback
//shadowJar {
//    archiveClassifier.set('')
//    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
//    manifest {
//        attributes 'Main-Class': 'com.pranav.temple_software.Launcher'
//    }
//}
