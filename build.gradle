plugins {
    id 'java'
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.1.0'
    // Plugin to simplify creating native installers
    id("org.beryx.jlink") version "3.1.1"
}

group = 'com.pranav'
version = '1.6.0'

repositories {
    mavenCentral()
    gradlePluginPortal()
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(23) // Ensure this matches your JDK version
    }
}

javafx {
    version = "21"
    modules = ['javafx.controls', 'javafx.fxml', 'javafx.graphics']
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

dependencies {

    implementation 'com.h2database:h2:2.2.224'
    implementation 'org.xerial:sqlite-jdbc:3.46.0.0'
}

application {
    mainModule = 'com.pranav.temple_software'
    mainClass = 'com.pranav.temple_software.Launcher'
}

// Configuration for the jpackage native installer
jlink {
    // This line explicitly sets the module path for jlink and jpackage
    addOptions('--module-path', configurations.runtimeClasspath.asPath)

    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
    launcher {
        name = 'TempleSoftware'
    }
    jpackage {
        imageName = 'TempleSoftware'
        appVersion = project.version
        vendor = 'Pranav'

        installerOptions = [
                '--copyright', 'Copyright Â© 2025 Pranav',
                '--win-menu',
                '--win-shortcut',
                '--win-upgrade-uuid', 'a2e1852b-12d4-4b9a-99de-93c8a415f7f3',
                '--win-per-user-install'
        ]

        installerType = 'msi'
    }
}
